## 实验10 整数规划

张一可 2021010793 计15

### 实验目的

1. 联系建立实际问题的整数规划模型
2. 掌握用 LINGO 软件求解整数规划问题

### 实验10-6 二次指派问题

#### 问题分析与模型建立

设矩阵 $B \in R^{n\times n}$ 表示员工之间的通话时长，$b_{ij}$ 表示员工 $i$ 和员工 $j$ 之间的每月通话时间。设矩阵 $C \in R^{n\times n}$ 表示城市之间的通话费率，$c_{ij}$ 表示城市 $i$ 和城市 $j$ 之间通话费率。

用矩阵 $A \in R^{n\times n}$ 表示员工的指派情况，$a_{ij} = 1$ 表示员工 $i$ 被分配到城市 $j$，$a_{ij}=0$ 表示员工 $j$ 不被分配到城市 $j$，则由题意，该矩阵应满足：
$$
\sum_{i=1}^{n}a_{ij} = 1, \forall j\\
\sum_{i=1}^{n}a_{ji} = 1, \forall j
$$
优化目标为使城市两两之间的总电话费用最小，即：
$$
min \ z = \sum_{i=1}^{n} \sum_{j=1}^{n} a_{ij} \sum_{k=i+1}^{n} \sum_{m=1}^{n} a_{km} b_{jm} c_{jm} \\
$$

由于 $a_{ij}\in \{0,1\}$，该问题为 0-1 规划问题，可用 LINGO 软件求解。

#### 代码实现

```
model:
sets:
n/1..10/:;
nsq(n,n): assign, ft;
endsets
data:
ft = 0 5 3 7 9 3 9 2 9 0
7 0 7 8 3 2 3 3 5 7
4 8 0 9 3 5 3 3 9 3
6 2 10 0 8 4 1 8 0 4
8 6 4 6 0 8 8 7 5 9
8 5 4 6 6 0 4 8 0 3
8 6 7 9 4 3 0 7 9 5
6 8 2 3 8 8 6 0 5 5
6 3 6 2 8 3 7 8 0 5
5 6 7 6 6 2 8 8 9 0;
enddata
min = @sum(n(i): @sum(n(j):assign(i,j)*@sum(n(k)|i#LT#k: 
    @sum(n(m): assign(k,m)*ft(i,k)*@if(j#GT#m,ft(j,m),ft(m,j));))));
@for(nsq: @bin(assign));
@for(n(i): @sum(n(j): assign(i,j))=1);
@for(n(j): @sum(n(i): assign(i,j))=1);
end
```

#### 实验结果与讨论

求解得到最优解为 $z = 1142$，即每个月通信总费用为 1142 元，员工的指派情况为：

员工 1 被指派到城市 9，员工 2 被指派到城市 1，员工 3 被指派到城市 8，员工 4 被指派到城市 3，员工 5 被指派到城市 6，员工 6 被指派到城市 7，员工 7 被指派到城市 2，员工 8 被指派到城市 5，员工 9 被指派到城市 4，员工 10 被指派到城市 10 

### 实验10-9 原油采购与加工

#### 问题分析与模型建立

设原油 A 的购买量为 $t$ 吨，不妨设其在 500t 以内、500t 到 1000t 之间、1000t 以上的量分别为 $t_1$, $t_2$, $t_3$，则满足：
$$
t = t_1 + t_2 + t_3\\
t1 \le 500, \ t_2 \le 500,\ t_3 \le 500 \\
t_2(t_1 - 500) = 0 \\
t_3(t_2 - 500) = 0
$$
购买费用为
$$
10000t_1 + 8000t_2 + 6000t_3
$$
设用库存中 $a$ 吨原油 A、$b$ 吨原油 B 生产汽油甲和汽油乙。设汽油甲产品中含有原油 A $c$ 吨，原油 B $d$ 吨；汽油乙产品中含有原油 A $e$ 吨，原油 B $f$ 吨，则满足约束条件：
$$
a \le 500,\ b \le 1000 \\
a+t = c+e,\ b=d+f \\
c\ge 0.5(c+d), \ e\ge 0.6(e+f)
$$
优化目标为最大化盈利，即
$$
max\ z = 4800(c+d) + 5600(e+f) - 10000t_1 - 8000t_2 - 6000t_3
$$
可以采用 LINGO 软件，通过约束决策变量是否取整数值，将该问题作为连续规划和整数规划分别求解。

#### 代码实现

连续规划问题的求解代码如下：

```
model:
max = 4800*(c+d)+5600*(e+f)-10000*t1-8000*t2-6000*t3;
t=t1+t2+t3;
t1<=500;
t2<=500;
t3<=500;
t2*(t1-500)=0;
t3*(t2-500)=0;
a<=500;
b<=1000;
a+t=c+e;
b=d+f;
c>=0.5*(c+d);
e>=0.6*(e+f);
end
```

采用整数规划求解时，增加约束条件使决策变量只能取整数值即可：

```
@gin(a);@gin(b);@gin(c);@gin(d);@gin(e);@gin(f);
@gin(t1);@gin(t2);@gin(t3);@gin(t);
```

#### 实验结果与讨论

采用连续规划解得最大收益为 $z=5000000$，决策变量取值为
$$
a = 500,\ b = 1000, \ t = 1000, \ t_1 = t_2 = 500, \ t_3 = 0\\
c = 0, \ d = 0,\ e = 1500,\ f = 1000
$$
采用整数规划解得的最大收益也为  $z=5000000$，决策变量取值为
$$
a = 500,\ b = 1000, \ t = 1000, \ t_1 = t_2 = 500, \ t_3 = 0\\
c = 0, \ d = 0,\ e = 1500,\ f = 1000
$$
连续规划和整数规划的求结果完全相同，然而连续规划的求解效率要高于整数规划，整数规划采用 MINLP 模型，迭代轮数为 938；而连续规划采用 NLP 模型，迭代轮数为 206。

#### 结论与分析

该公司应该购买原油 A 总共 1000 吨，并且使用所有的原油 A 和原油 B 全部用来生产乙汽油，能够使得利润最大，为 5 百万元。

### 实验10-12 下料问题

#### 问题分析与模型建立

设选择 1, 2, 3, 4 四种模式进行裁剪的数量分别为 $x_1$, $x_2$, $x_3$, $x_4$，满足约束条件：
$$
x_1+x_2+x_3\le 50000\\
x_4\le 20000\\
1.5x_1+2x_2+x_3+3x_4\le 40\times 3600\\
$$
四种方式产生的原料数量和余料损失如下：

1. 模式 1 产生易拉罐的上盖/下底面 10 个，罐身 1 个，余料损失为 $t_1 = 24^2-10\times \pi\times (2.5)^2-50\pi$
2. 模式 2 产生易拉罐的上盖/下底面 4 个，罐身 2 个，余料损失为 $t_2 = 24^2-4\times \pi\times (2.5)^2-2\times 50\pi$
3. 模式 3 产生易拉罐的上盖/下底面 16 个，余料损失为 $t_3 = 24^2-16\times \pi\times (2.5)^2$
4. 模式 4 产生易拉罐的上盖/下底面 5个，罐身 4 个，余料损失为 $t_4 = 28\times 32-5\times \pi\times (2.5)^2-4\times 50\pi$

因此，总共产生易拉罐上盖/下底 $a = 10x_1+4x_2+16 x_3+5x_4$ 个，产生罐身 $b = x_1+2x_2+4x_4$ 个，组装称为易拉罐 $c= min\{\frac{a}{2}, b\}$ 个，额外的余料损失为
$$
d=(a-2c)\times \pi \times (2.5)^2 + (b-c)\times 50\pi
$$
优化目标为最大化收益，即
$$
max\ z= 0.1c - 0.001\times(t_1x_1+t_2x_2+t_3x_3+t_4x_4+d)
$$
利用 LINGO 软件求解该整数非线性规划问题。

#### 代码实现

```
model:
sets:
n/1..4/:x,loss,circ,rect,time;
endsets
data:
circ=10 4 16 5;
rect=1 2 0 4;
time=1.5 2 1 3;
enddata
pi=3.1415926;
loss(1)=24*24-10*pi*2.5*2.5-50*pi;
loss(2)=24*24-4*pi*2.5*2.5-2*50*pi;
loss(3)=24*24-16*pi*2.5*2.5;
loss(4)=28*32-5*pi*2.5*2.5-4*50*pi;
x(1)+x(2)+x(3)<=50000;
x(4)<=20000;
@sum(n(i):time(i)*x(i))<=40*3600;
a=@sum(n(i):circ(i)*x(i));
b=@sum(n(i):rect(i)*x(i));
c*2<=a;
c<=b;
@gin(a);@gin(b);@gin(c);
@for(n(i):@gin(x(i)););
extra_loss=(a-2*c)*pi*2.5*2.5+(b-c)*50*pi;
max=0.1*c-0.001*(@sum(n(i):x(i)*loss(i))+extra_loss);
end
```

#### 实验结果与讨论

解得最大收益为 4298 元，分别采用模式 1, 2, 3, 4 压制 0, 40125, 3750, 20000 张，每周生产易拉罐 160250 个。

#### 总结与分析

将条件  $c= min\{\frac{a}{2}, b\}$ 转化为两个线性不等式 $2c\le a$，$b\le a$ 有助于求解。

### 总结与收获

通过实验，我更加熟练地掌握了根据实际问题建立整数规划模型，并利用 LINGO 软件编程求解的能力，我也通过实践温习了课上讲到的将非线性问题转化为先行不等式从而灵活求解的方法。